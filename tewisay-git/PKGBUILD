pkgname=tewisay-git
_gitname='tewisay'
pkgver=0.49.9ae12b5
pkgrel=1
pkgdesc='Cowsay replacement with unicode and partial ansi escape support.'
url='https://github.com/neeee/tewisay'
license=('custom:CC0')
makedepends=('go' 'git' 'asciidoc')
optdepends=('cowsay: default cowfiles')
arch=('i686' 'x86_64' 'ARM')
source=('git://github.com/neeee/tewisay.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	echo "0.$(git rev-list --count HEAD).$(git describe --always )"
}

build() {
	cd "$srcdir/$_gitname"
	GOPATH="$srcdir"
	go get -d .
	go build
	a2x --doctype manpage --format manpage tewisay.1.txt
}

package() {
	cd "$srcdir/$_gitname"
	install -D -m755 tewisay  "$pkgdir/usr/bin/tewisay"
	install -D -m644 _tewisay "$pkgdir/usr/share/zsh/site-functions/_tewisay"
	install -d "$pkgdir/usr/man/man1"
	install -D -m644 tewisay.1 "$pkgdir/usr/man/man1/tewisay.1"
	install -D -m644 COPYING  "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	install -D -m644 cows/COPYING \
		"$pkgdir/usr/share/licenses/$pkgname/COPYING.tewis"
	install -d "$pkgdir/usr/share/cows"
	install -D -m644 cows/*.cow "$pkgdir/usr/share/cows"
	ln -s "tewisay" "$pkgdir/usr/bin/tewithink"
}
